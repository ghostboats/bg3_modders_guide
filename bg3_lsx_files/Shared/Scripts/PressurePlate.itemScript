INIT
	ITEM:__Me
	EXTERN FLOAT:%MaxWeight=50

	FLOAT:%CurrentPercentage=0
	FLOAT:%PreviousPercentage=0
	FLOAT:%NewWeight=0

EVENTS

EVENT Initialize
ON
	OnInit()
ACTIONS
	StartTimer("UpdatePressurePlate",0.1,-1, 1)

EVENT Shutdown
ON
	OnShutdown()
	OnItemDestroying(__Me)
ACTIONS
	StopTimer("UpdatePressurePlate")

EVENT Update
VARS
	FLOAT:_PreviousWeight
ON
	OnTimer("UpdatePressurePlate")
ACTIONS
	IF "!c1"
		ItemIsMoving(__Me)
	THEN
		Set(%NewWeight,0)
		IterateCharactersOnObject(__Me,"PressurePlateCheck")
		IterateItemsOnObject(__Me,"PressurePlateCheck")	
		Set(%PreviousPercentage,%CurrentPercentage)
		Set(%CurrentPercentage,%NewWeight)
		Divide(%CurrentPercentage,%MaxWeight)		
		Clamp(%CurrentPercentage,0,1)		
		IF "!c1"
			IsEqual(%CurrentPercentage,%PreviousPercentage)
		THEN
			ItemEvent(__Me,"PressurePlateChange")
		ENDIF
	ENDIF
	
EVENT WeightCalcCharacter
VARS
CHARACTER:_Char
FLOAT:_Weight
ON
	OnIterateCharacter(_Char,"PressurePlateCheck")
ACTIONS
	IF "!c1&c2&!c3&!c4"
		IsEqual(_Char,null)
		CharacterGetStat(_Weight,_Char,Weight)
		CharacterIsIgnoringGroundSurface(_Char)
		CharacterIsDead(_Char)		
	THEN		
		Add(%NewWeight,_Weight)
	ENDIF

EVENT WeightCalcItem
VARS
ITEM:_Item
FLOAT:_Weight
ON
	OnIterateItem(_Item,"PressurePlateCheck")
ACTIONS
	IF "!c1&!c2&!c3&!c4&c5"		
		IsEqual(_Item,null)
		ItemIsMoving(_Item)
		ItemIsFalling(_Item)
		ItemIsDestroyed(_Item)
		ItemGetStat(_Weight,_Item,Weight)
	THEN
		Add(%NewWeight,_Weight)
	ENDIF
